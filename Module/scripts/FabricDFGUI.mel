global proc hideWidget()
{
  if (`dockControl -exists FabricDFGUIPane` && `dockControl -q -io FabricDFGUIPane`)
  {
    evalDeferred "if (`dockControl -exists FabricDFGUIPane` && `dockControl -q -io FabricDFGUIPane`) catchQuiet(`deleteUI FabricDFGUIPane`)";
  }
}

global proc showDFGWidget(string $pluginName)
{
  string $mayaVersion = `about -version`;
  $mayaVersion = `substring $mayaVersion 1 4`;
  string $uiDirectory;
  if($mayaVersion == "2012")
  {
    string $pluginPath = `pluginInfo -q -path $pluginName`;
    string $pluginDir = `dirname $pluginPath`;
    string $parentDir = `dirname $pluginDir`;
    $uiDirectory = $parentDir+"/ui"; 
  }
  else
    $uiDirectory = `getModulePath -moduleName $pluginName` + "/ui";

  if (`dockControl -exists FabricDFGUIPane`)
  {
    catchQuiet(`showWindow FabricDFGUIPane`);
  }
  else
  {
    string $layOut1 = `paneLayout -configuration "single"`;
    string $widget = `loadUI -uiFile ($uiDirectory+"/FabricDFGWidget.ui")`;
    control -e -parent $layOut1 $widget;
    dockControl -allowedArea "all" -area "bottom" -floating on -content $layOut1 -label "Fabric Engine - Canvas" -width 1000 -height 500 -vcc "hideWidget();" FabricDFGUIPane;
  }
}

global proc deleteDFGWidget()
{
  if (`window -exists FabricDFGUIPane` == 1)
  {
    deleteUI FabricDFGUIPane;
  }
}
